If not(isclear($itasks.$findname('core')))
	Do method stopApp ('core')
	Do $cinst.$openAllLibraries()     ;; wenn nur der core laufen würde, sollte auch nur diese lib geöffnet sein
End If
If not(isclear($itasks.$findname('iMed')))
	Do method stopApp ('iMed')
End If

If pImportCore
	Do $cinst.$getAllGitFolders(iPath2Core) Returns lCoreLibs
	Do lListMaster.$copydefinition(lCoreLibs)
End If

If pImportPlugins
	Do $cinst.$getAllGitFolders(iPath2Plugins) Returns lPluginsList
	Do lListMaster.$copydefinition(lPluginsList)
End If
Do lListMaster.$merge(lCoreLibs)
Do lListMaster.$merge(lPluginsList)

Do $cinst.$defineErrorMaster(lMasterErrors)
Do $cinst.$defineErrorMaster(lMasterWarnings)

Do lListMaster.$sort(low($ref.name))     ;; Alphabetisch sortieren

For l1 from 1 to lListMaster.$linecount step 1
	Calculate lImportLibName as left(lListMaster('name',l1),rpos('.git',lListMaster('name',l1))-1)
	If lImportLibName='GitImportExport'
		Jump to start of loop
	End If
	
	Calculate lTmpLibPath as replace(lListMaster('fullname',l1),'.git','_new.lbs')
	Calculate lLibPath as replace(lListMaster('fullname',l1),'.git','.lbs')
	
	Do FileOps.$deletefile(lTmpLibPath)
	Do $root.$importjson(lListMaster('fullname',l1),lTmpLibPath,lErrors,lWarnings) Returns lStatus
	If lErrors.$linecount<1&lStatus
		If FileOps.$doesfileexist(lTmpLibPath)
			Close library {[lImportLibName]}
			Do FileOps.$deletefile(lLibPath) Returns lFileOpsError
			If lFileOpsError=kFileOpsOK|lFileOpsError=kFileOpsFileNotFound     ;; kFileOpsFileNotFound-> im Fall einer neuen Libary existiert diese nicht schon vorher
				Do FileOps.$movefile(lTmpLibPath,lLibPath) Returns lFileOpsError
				If lFileOpsError=kFileOpsOK&FileOps.$doesfileexist(lLibPath)
					Open library (Do not close others,Enable conversion by runtime,Do not open startup task) {[lLibPath]}
				Else
					;  Breakpoint {Importierte Library kann nich geöffnet werden}
					No/Yes message Libary konnte nicht importiert werden {Die Libary [lImportLibName] konnte nicht importiert werden, da da die bestehende .lbs Datei nicht gelöscht werden konnte. Möchtest du trotzdem mit dem Import weiterer Libraries fortfahren?}
					If flag false
						Do lMasterErrors.$merge(lErrors)
						Do lMasterWarnings.$merge(lWarnings)
						Break to end of loop
					End If
				End If
			End If
		End If
	Else
		No/Yes message Libary konnte nicht importiert werden {Die Libary [lImportLibName] konnte nicht importiert werden. Bitte die Fehlerliste ansehen. Möchtest du trotzdem mit dem Import weiterer Libraries fortfahren?}
		If flag false
			Do lMasterErrors.$merge(lErrors)
			Do lMasterWarnings.$merge(lWarnings)
			Break to end of loop
		End If
	End If
	Do lMasterErrors.$merge(lErrors)
	Do lMasterWarnings.$merge(lWarnings)
End For

If lMasterErrors.$linecount=0
	If pImportCore
		Do method writeBaseVersion (iPath2Core)
	End If
	If pImportPlugins
		Do method writeBaseVersion (iPath2Plugins)
	End If
End If

Do $cinst.$showIssuesOverview(lMasterErrors,lMasterWarnings)